BACKUP ~bg2eetrans/backup~
AUTHOR ~Isaya~

// Version stated in WeiDU.log
// The last part refers to the game version used as a reference to find texts
VERSION ~v0.7.16.6~

// Tells WeiDU where the tra files are, according to language choice (%s refers to the language name)
AUTO_TRA ~bg2eetrans/language/%s~
// Comment to prevent a failure in the notepad++ highlither because of the ~%~ character above

ALWAYS

END	// ALWAYS


// Language choice : follow the rule, use the directory names from BGEE
// ATTENTION!!! The order of languages here and in bg2eetrans.tp2 shall remain consistent all the time
LANGUAGE ~Francais~
	     ~fr_FR~
         ~bg2eetrans/language/fr_FR/setup.tra~



// Texts recovered from BGII-ToB
BEGIN @1000002
DESIGNATED 1001
REQUIRE_PREDICATE (FILE_EXISTS ~lang/%LANGUAGE%/bg2eetrans.mrk~) @1001003 // Initialisation failed or a translation file is already present
REQUIRE_COMPONENT ~bg2eetrans.tp2~ ~0~ @1001004 // Block attempt to bypass the initialisation part

LOAD_TRA ~bg2eetrans/language/%LANGUAGE%/dialogToB.tra~
INCLUDE  ~bg2eetrans/tob.tph~

// Texts found by visual check (not significant differences that fooled Translation Retriver algorithm)
// For french: not required anymore, moved into tob.tph
//INCLUDE ~bg2eetrans/tob_manual.tph~



// Texts recovered from BGEE
BEGIN @1000003
DESIGNATED 1002
REQUIRE_PREDICATE (FILE_EXISTS ~lang/%LANGUAGE%/bg2eetrans.mrk~) @1001003 // Initialisation failed or a translation file is already present
REQUIRE_COMPONENT ~bg2eetrans.tp2~ ~0~ @1001004 // Block attempt to bypass the initialisation part

LOAD_TRA ~bg2eetrans/language/%LANGUAGE%/dialogBGEE.tra~
INCLUDE  ~bg2eetrans/bgee.tph~

// Texts found by visual check (not significant differences that fooled Translation Retriver algorithm)
INCLUDE ~bg2eetrans/bgee_manual.tph~



// Menu for the language, including credits for the translation recovery
BEGIN @1000004
DESIGNATED 1003
REQUIRE_PREDICATE (FILE_EXISTS ~lang/%LANGUAGE%/bg2eetrans.mrk~) @1001003 // Initialisation failed or a translation file is already present
REQUIRE_COMPONENT ~bg2eetrans.tp2~ ~0~ @1001004 // Block attempt to bypass the initialisation part

ACTION_IF (FILE_EXISTS ~engine.lua~) THEN BEGIN
	// Installing on BG2EE V2.x: we need to update BGEE.lua to add the language in the options menu
	// Test to check for BGEE.lua existence: it will fail with an old WeiDU (requires > 239, either 240 or at least 23901)
	ACTION_IF (FILE_EXISTS_IN_GAME ~BGEE.lua~) THEN BEGIN
		// Add a description of the "translation for the menu
		//NEXT_STRREF
		//STRING_SET_EVALUATE value newValue list [ USING traFile ]
		OUTER_SET valeur1 = RESOLVE_STR_REF(@1002001)
		OUTER_SET valeur2 = RESOLVE_STR_REF(@1002002)
		OUTER_SPRINT transDesc1 ~%valeur1%~
		OUTER_SPRINT transDesc2 ~%valeur2%~
		ACTION_IF ( ~%LANGUAGE%~ STRING_EQUAL_CASE ~fr_FR~ ) THEN BEGIN
			// Add the chosen language to BG2EE
			COPY_EXISTING ~BGEE.lua~ ~override/BGEE.lua~
				REPLACE_TEXTUALLY ~\(	{'en_US', 99385, 99386, 'English'},\)~ ~\1
	{'fr_FR', %transDesc1%, %transDesc2%, 'Fran√ßais'},~
				UNLESS ~'fr_FR'~
			// Add the translated interface file
			// Must be in override so that mod that change the UI can patch it
			// (at this time, WeiDU can't patch it in a language override)
			ACTION_IF (NOT FILE_EXISTS_IN_GAME ~L_fr_FR.lua~) THEN BEGIN
				COPY ~bg2eetrans/language/%LANGUAGE%/L_fr_FR.lua~ ~override/L_fr_FR.lua~
			END
		END
		// Add additional language here
	END ELSE BEGIN
		FAIL @1001005
	END
END



// Verified translation for BG2EE
BEGIN @1000005
DESIGNATED 1004
REQUIRE_PREDICATE (FILE_EXISTS ~lang/%LANGUAGE%/bg2eetrans.mrk~) @1001003 // Initialisation failed or a translation file is already present
REQUIRE_COMPONENT ~bg2eetrans.tp2~ ~0~ @1001004 // Block attempt to bypass the initialisation part

// Traduction de textes BG2EE compris entre 74107 et la fin

PRINT @1000601
LOAD_TRA ~bg2eetrans/language/%LANGUAGE%/Sorts.tra~
INCLUDE  ~bg2eetrans/Sorts.tph~

PRINT @1000741
LOAD_TRA ~bg2eetrans/language/%LANGUAGE%/Rasaad-SoA.tra~
INCLUDE  ~bg2eetrans/Rasaad-SoA.tph~

PRINT @1000602
LOAD_TRA ~bg2eetrans/language/%LANGUAGE%/Personnages_Sous-titres.tra~
INCLUDE  ~bg2eetrans/Personnages_Sous-titres.tph~

PRINT @1000603
LOAD_TRA ~bg2eetrans/language/%LANGUAGE%/Objets.tra~
INCLUDE  ~bg2eetrans/Objets.tph~

PRINT @1000604
LOAD_TRA ~bg2eetrans/language/%LANGUAGE%/Cartes.tra~
INCLUDE  ~bg2eetrans/Cartes.tph~

PRINT @1000691
LOAD_TRA ~bg2eetrans/language/%LANGUAGE%/Divers.tra~
INCLUDE  ~bg2eetrans/Divers.tph~

PRINT @1000692
LOAD_TRA ~bg2eetrans/language/%LANGUAGE%/BG2EE_74107-fin.tra~
INCLUDE  ~bg2eetrans/BG2EE_74107-fin.tph~

// Test for V2.5 presence: only add new texts if present
// (otherwise STRING_SET will fail on V2.3 if trying to update non-existant strings)
// Use Priest of Tempus file to detect V2.5
ACTION_IF (FILE_EXISTS_IN_GAME ~OHTEMPUS.2DA~) THEN BEGIN
	PRINT @1000801
	LOAD_TRA ~bg2eetrans/language/%LANGUAGE%/BG2EE_2_5.tra~
	INCLUDE ~bg2eetrans/BG2EE_2_5.tph~
END
